<h3>6.2 <code>defaults</code> promises in <samp><span>*</span></samp></h3>

<p><br>

   <p>Defaults promises are related to variables. If a variable or parameter in a promise bundle is undefined, or its value is defined to be invalid, a default value can be promised instead.

   <p><br>

   <p>Note carefully that CFEngine does not use Perl semantics: i.e. undefined variables do not map to the empty string, they remain as variables for possible future expansion.  
   <p>Some variables might be defined but still contain unresolved variables. To handle this you will need to match the <code>$(abc)</code> form of the variables.

<pre>
body common control
{
bundlesequence => { "main" };
}

bundle agent main
{
methods:

  "example"  usebundle => test("one","x","","$(four)");

}

bundle agent test(a,b,c,d)
{
defaults:

 "a" string => "default a", if_match_regex => "";
 "b" string => "default b", if_match_regex => "x";
 "c" string => "default c", if_match_regex => "";
 "d" string => "default d", if_match_regex => "\$\([a-zA-Z0-9_.]+\)";

reports:

   !nothing::

   "a = '$(a)', b = '$(b)', c = '$(c)' d = '$(d)'";
}

</pre>
Another example:

<pre>
bundle agent example

{     
defaults:

  "X" string => "I am a default value";
  "Y" slist => { "I am a default list item 1", "I am a default list item 2" };

methods:

 "example" usebundle => mymethod("","bbb");

reports:

 !xyz::

   "The default value of X is $(X)";
   "The default value of Y is $(Y)";
}

###########################################################

bundle agent mymethod(a,b)

{
vars:

  "no_return" string => "ok"; # readfile("/dont/exist","123");

defaults:

  "a" string => "AAAAAAAAA",   if_match_regex => "";

  "b" string => "BBBBBBBBB",   if_match_regex => "";

  "no_return" string => "no such file";

reports:

  !xyz::

     "The value of a is $(a)";
     "The value of b is $(b)";

     "The value of no_return is $(no_return)";

}

</pre>

   <p><br>

<ul>
<li><a>if_match_regex in defaults</a>
<li><a>string in defaults</a>
<li><a>slist in defaults</a>
</ul>

<div>
<a></a>
<a></a>

</div>

<h4>6.2.1 <code>if_match_regex</code></h4>

<p><b>Type</b>: string

<p><b>Allowed input range</b>: (arbitrary string)

<p><b>Synopsis</b>: If this regular expression matches the current value of the variable, replace it with default

<p><b>Example</b>:<br>
<br>

<pre>bundle agent mymethod(a,b)
{
defaults:

  "a" string => "AAAAAAAAA",   if_match_regex => "";
  "b" string => "BBBBBBBBB",   if_match_regex => "";
}
</pre>

<p><b>Notes</b>:<br>
<br>

   <p>If a parameter or variable is already defined in the current context, and the value matches this regular expression, it will be deemed invalid and replaced with the default value.

<div>
<a></a>

</div>

<h4>6.2.2 <code>string</code></h4>

<p><b>Type</b>: string

<p><b>Allowed input range</b>: (arbitrary string)

<p><b>Synopsis</b>: A scalar string

<p><b>Example</b>:<br>
<br>

<pre>
vars:

 "xxx"    string => "Some literal string...";

 "yyy"    string => readfile( "/home/mark/tmp/testfile" , "33" );

</pre>

<p><b>Notes</b>:<br>
<br>

   <p>In previous versions of CFEngine lists were represented (as in the shell) using separated scalars; similar to the PATH variable. In CFEngine 3 lists are kept as an independent type. 

<div>
<a></a>

</div>

<h4>6.2.3 <code>slist</code></h4>

<p><b>Type</b>: slist

<p><b>Allowed input range</b>: (arbitrary string)

<p><b>Synopsis</b>: A list of scalar strings

<p><b>Example</b>:<br>
<br>

<pre>vars:

 "xxx"    slist  => {  "literal1",  "literal2" };

 "yyy"    slist  => { 
                    readstringlist(
                                  "/home/mark/tmp/testlist",
                                  "#[a-zA-Z0-9 ]*",
                                  "[^a-zA-Z0-9]",
                                  15,
                                  4000
                                  ) 
                    };

 "zzz"    slist  => { readstringlist("/home/mark/tmp/testlist2","#[^\n]*",",",5,4000) };


</pre>

<p><b>Notes</b>:<br>
<br>
Some functions return <code>slist</code>s (see <a>Introduction to functions</a>), and an <code>slist</code> may contain the values copied from another <code>slist</code>, <code>rlist</code>, or <code>ilist</code> (see <a>List variable substitution and expansion</a>, see <a>policy in vars</a>).

<div>
<a></a>

</div>


