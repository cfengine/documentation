<h3>11.89 Function selectservers</h3>

<p><b>Synopsis</b>: selectservers(arg1,arg2,arg3,arg4,arg5,arg6) returns type <b>int</b>

   <p><br>
   <i>arg1</i> : The identifier of a cfengine list of hosts or addresses to contact, <i>in the range</i> @[(][a-zA-Z0-9]+[)]
<br>
   <i>arg2</i> : The port number, <i>in the range</i> 0,99999999999
<br>
   <i>arg3</i> : A query string, <i>in the range</i> .*
<br>
   <i>arg4</i> : A regular expression to match success, <i>in the range</i> .*
<br>
   <i>arg5</i> : Maximum number of bytes to read from server, <i>in the range</i> 0,99999999999
<br>
   <i>arg6</i> : Name for array of results, <i>in the range</i> [a-zA-Z0-9_$(){}\[\].:]+
<br>

<p>Select tcp servers which respond correctly to a query and return their number, set array of names

<p><b>Example</b>:<br>
<br>

<pre>
body common control

{
bundlesequence  => { "test"  };
}

###########################################################

bundle agent test

{     
vars:

 "hosts" slist => { "slogans.iu.hio.no", "eternity.iu.hio.no", "nexus.iu.hio.no" };
 "fhosts" slist => { "www.cfengine.com", "www.cfengine.org" };
 
 "up_servers" int =>  selectservers("@(hosts)","80","","","100","alive_servers");
 "has_favicon" int =>
	    selectservers(
	        "@(hosts)", "80",
		"GET /favicon.ico HTTP/1.0$(const.n)Host: www.cfengine.com$(const.n)$(const.n)",
		"(?s).*OK.*",
		"200", "favicon_servers");

classes:

  "someone_alive" expression => isgreaterthan("$(up_servers)","0");

  "has_favicon" expression => isgreaterthan("$(has_favicon)","0");

reports:

  cfengine_3::
    "Number of active servers $(up_servers)";

  someone_alive::
    "First server $(alive_servers[0]) fails over to $(alive_servers[1])";

  has_favicon::
    "At least $(favicon_servers[0]) has a favicon.ico";

}


</pre>

<p><b>Notes</b>:<br>
<br>

   <p>This function selects all the TCP ports that are active and functioning from an ordered list and builds an array of their names.  This allows us to select a current list of failover alternatives that are pretested.

     <dl>
<dt><samp><span>hostlist</span></samp><dd>A list of host names or IP addresses to attempt to connect to.

     <br><dt><samp><span>port</span></samp><dd>The port number for the service.

     <br><dt><samp><span>sendstr</span></samp><dd>An optional string to send to the server to elicit a response.  If
<code>sendstr</code> is empty, then no query is sent to the server.

     <br><dt><samp><span>regex_on_reply</span></samp><dd>If a string is sent, this regex is anchored, meaning it must match the entire resulting reply (see <a>Anchored vs. unanchored regular expressions</a>). If there is a multi-line response from the server, special care must be taken to ensure that you match the newlines, too (note the use of <code>(?s)</code> in the example above, which allows <samp><span>.</span></samp> to also match newlines in the multi-line HTTP response). If <code>regex_on_reply</code> is empty, then no reply-checking is performed (and any server reply is deemed to be satisfactory).

     <br><dt><samp><span>maxbytesread_reply</span></samp><dd>The maximum number of bytes to read as the server's reply.

     <br><dt><samp><span>array_name</span></samp><dd>The name of the array to build containing the names of hosts that pass the above tests. The array is ordered <code>array_name[0],..</code> etc. 
</dl>

<div>
<a></a>

</div>


