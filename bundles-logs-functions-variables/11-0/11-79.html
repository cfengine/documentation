<h3>11.79 Function regcmp</h3>

<p><b>Synopsis</b>: regcmp(arg1,arg2) returns type <b>class</b>

   <p><br>
   <i>arg1</i> : Regular expression, <i>in the range</i> .*
<br>
   <i>arg2</i> : Match string, <i>in the range</i> .*
<br>

<p>True if arg1 is a regular expression matching that matches string arg2

<p><b>Example</b>:<br>
<br>

<pre>
bundle agent subtest(user)

{
classes:

  "invalid" not => regcmp("[a-z]{4}","$(user)");

reports:

 !invalid::

  "User name $(user) is valid at exactly 4 letters";

 invalid::

  "User name $(user) is invalid";
}

</pre>

<p><b>Notes</b>:<br>
<br>

   <p>Compares a string to a regular expression.

<p><b>ARGUMENTS</b>:

     <dl>
<dt><samp><span>regex</span></samp><dd>A regular expression to match the content. The regular expression
is anchored, meaning it must match the complete content (See <a>Anchored vs. unanchored regular expressions</a>).

     <br><dt><samp><span>string</span></samp><dd>Test data for the regular expression. 
</dl>

   <p>If there are multiple-lines in the data, it is necessary to code these explicitly, as regular expressions do not normally match the end of line as a regular character (they only match end of string). You can do this using either standard regular expression syntax or using the additional features of PCRE (where <code>(?ms)</code> changes the way that <samp><span>.</span></samp>, <samp><span>^</span></samp> and <samp><span>$</span></samp> behave), e.g.

<pre>     
     body common control
     {
     bundlesequence = { "example" };
     }
     
     bundle agent example
     {
     vars:
     
       "x" string = "
     NAME: apache2 - Apache 2.2 web server
     CATEGORY: application
     ARCH: all
     VERSION: 2.2.3,REV=2006.09.01
     BASEDIR: /
     VENDOR: http://httpd.apache.org/ packaged for CSW by Cory Omand
     PSTAMP: comand@thor-20060901022929
     INSTDATE: Dec 14 2006 16:05
     HOTLINE: http://www.blastwave.org/bugtrack/
     EMAIL: comand@blastwave.org
     STATUS: completely installed
     ";
     
     classes:
     
       "pkg_installed" expression = regcmp("(.*\n)*STATUS:\s+completely installed\n(.*\n)*",$(x));
     
       "base_is_root" expression = regcmp("(?ms).*^BASEDIR:\s+/$.*", $(x));
     
     reports:
     
       pkg_installed::
     
         "installed";
     
       base_is_root::
     
         "in root";
     }
     
     
</pre>
   <div>
<a></a>

</div>


